// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including RAM, memory mapped screen, and memory mapped keyboard.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 (0x0000 to 0x3FFF) results in accessing the RAM;
 * Access to address 16384 (100000000000000) to 24575 (0x4000 to 0x5FFF) results in accessing the Screen memory map;
 * Access to address 24576 (110000000000000) (0x6000) results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    Not(in=address[13], out=not13bit);
    Not(in=address[14], out=notMSB);
    And(a=load,b=notMSB,out=loadRAM);
    And(a=load,b=address[14], out=screenAndKeyboardRegisters);
    And(a=screenAndKeyboardRegisters, b=not13bit, out=loadScreen);
    Screen(in=in, load=loadScreen, address=address[0..12], out=screenOut);
    Keyboard(out=kbdOut);
    RAM16K(in=in,load=loadRAM,address=address[0..13],out=ramOut);
    Mux16(a=kbdOut,b=screenOut, sel=not13bit, out=kbdOrScreenOut);
    Mux16(a=kbdOrScreenOut,b=ramOut,sel=notMSB,out=out);
}
